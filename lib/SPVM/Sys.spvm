# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Sys {
  version "0.500";
  
  use Sys::OS;
  use Sys::Env;
  use Sys::FileTest;
  use Sys::Ioctl;
  use Sys::User;
  use Sys::Time;
  use Sys::Process;
  use Sys::IO::Stat;
  use Sys::Socket;
  use Sys::Select;
  use Sys::IO::FileStream;
  use Format;
  use Sys::IO::Constant as IO;
  use Sys::Signal;
  
  static method osname : string () {
    my $osname : string;
    
    if (Sys::OS->defined("__linux__")) {
      $osname = "linux";
    }
    elsif (Sys::OS->defined("_WIN32")) {
      $osname = "MSWin32";
    }
    elsif (Sys::OS->defined("__FreeBSD__")) {
      $osname = "freebsd";
    }
    elsif (Sys::OS->defined("__OpenBSD__")) {
      $osname = "openbsd";
    }
    elsif (Sys::OS->defined("__solaris")) {
      $osname = "solaris";
    }
    elsif (Sys::OS->defined("__sun")) {
      $osname = "solaris";
    }
    elsif (Sys::OS->defined("__APPLE__")) {
      $osname = "darwin";
    }
    else {
      die "The OS name cannot be got.";
    }
    
    return $osname;
  }
  
  static method env : string ($name : string) {
    my $env_value = Sys::Env->getenv($name);
    return $env_value;
  }
  
  static method A : double ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    
    my $result_time = $stat->A;
    
    return $result_time;
  }
  
  static method C : double ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    my $result_time = $stat->C;
    
    return $result_time;
  }
  
  static method M : double ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    
    my $result_time = $stat->M;
    
    return $result_time;
  }
  
  static method O : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->O;
    }
    
    return $ok;
  }
  
  static method R : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->R;
    }
    
    return $ok;
  }
  
  static method S : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    
    my $ok = $stat->S;
    
    return $ok;
  }
  
  static method W : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->W;
    }
    
    return $ok;
  }
  
  static method X : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->X;
    }
    
    return $ok;
  }
  
  static method b : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Block device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->b;
    }
    
    return $ok;
  }
  
  static method c : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->c;
    }
    
    return $ok;
  }
  
  static method d : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->d;
    }
    
    return $ok;
  }
  
  static method e : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->e;
    }
    
    return $ok;
  }
  
  static method f : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->f;
    }
    
    return $ok;
  }
  
  static method g : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->g;
    }
    
    return $ok;
  }
  
  static method k : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->k;
    }
    
    return $ok;
  }
  
  static method l : int ($file : string) {
    
    my $ok = 0;
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    if (Sys::OS->is_windows) {
      $ok = Sys::IO::Windows->is_symlink($file);
    }
    else {
      my $stat = Sys::IO::Stat->new;
      eval { Sys::IO::Stat->lstat($file, $stat); }
      
      unless ($@) {
        $ok = $stat->l;
      }
    }
    
    return $ok;
  }
  
  static method o : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    my $ok = 0;
    unless ($@) {
      $ok = $stat->o;
    }
    return $ok;
  }
  
  static method p : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # FIFO/PIPE
    my $ok = 0;
    unless ($@) {
      $ok = $stat->p;
    }
    
    return $ok;
  }
  
  static method r : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->r;
    }
    
    return $ok;
  }
  
  static method s : long ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    
    my $size = $stat->s;
    
    return $size;
  }
  
  static method u : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->u;
    }
    
    return $ok;
  }
  
  static method w : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->w;
    }
    
    return $ok;
  }
  
  static method x : int ($file : string) {
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys::IO::Stat->new;
    eval { Sys::IO::Stat->stat($file, $stat); }
    
    # Character device
    my $ok = 0;
    unless ($@) {
      $ok = $stat->x;
    }
    
    return $ok;
  }
  
  static method z : int ($file : string) {
    
    unless ($file) {
      die "\$file must be defined.";
    }
    
    my $stat = Sys->stat($file);
    
    my $ok = $stat->z;
    
    return $ok;
  }
  
  static method time : long () {
    my $time = Sys::Time->time();
    return $time;
  }
  
  static method localtime : Sys::Time::Tm ($time : long) {
    my $tm = Sys::Time->localtime($time);
    return $tm;
  }
  
  static method gmtime : Sys::Time::Tm ($time : long) {
    my $tm = Sys::Time->gmtime($time);
    return $tm;
  }
  
  static method process_id : int () {
    my $process_id = Sys::Process->getpid();
    return $process_id;
  }
  
  static method stat : Sys::IO::Stat ($path : string) {
    my $stat = Sys::IO::Stat->new;
    Sys::IO::Stat->stat($path, $stat);
    return $stat;
  }
  
  static method fstat : Sys::IO::Stat ($fd : int) {
    my $stat = Sys::IO::Stat->new;
    Sys::IO::Stat->fstat($fd, $stat);
    return $stat;
  }
  
  static method chdir : int ($path : string) {
    Sys::IO->chdir($path);
    my $success = 1;
    return $success;
  }
  
  static method chmod : int ($mode :int, $path : string) {
    Sys::IO->chmod($path, $mode);
    my $success = 1;
    return $success;
  }
  
  static method mkdir : int ($path : string, $mode : int) {
    Sys::IO->mkdir($path, $mode);
    my $success = 1;
    return $success;
  }
  
  static method umask : int ($mode : int) {
    my $current_umask = Sys::IO->umask($mode);
    
    return $current_umask;
  }
  
  static method rmdir : int ($path : string) {
    Sys::IO->rmdir($path);
    my $success = 1;
    return $success;
  }
  
  static method opendir : int ($dh_ref : Sys::IO::DirStream[], $dir : string) {
    
    my $dh = Sys::IO->opendir($dir);
    
    $dh_ref->[0] = $dh;
    
    my $success = !($dh == undef);
    
    return $success;
  }
  
  static method closedir : int ($dirp : Sys::IO::DirStream) {
    Sys::IO->closedir($dirp);
    
    my $success = 1;
    
    return $success;
  }
  
  static method close : int ($stream : Sys::IO::FileStream) {
    Sys::IO->fclose($stream);
    
    my $success = 1;
    
    return $success;
  }
  
  static method unlink : int ($pathname : string) {
    
    if (Sys::OS->is_windows) {
      Sys::IO::Windows->unlink($pathname);
    }
    else {
      Sys::IO->unlink($pathname);
    }
    
    my $success = 1;
    
    return $success;
  }
  
  static method rename : int ($oldpath : string, $newpath : string) {
    if (Sys::OS->is_windows) {
      Sys::IO::Windows->rename($oldpath, $newpath);
    }
    else {
      Sys::IO->rename($oldpath, $newpath);
    }
    
    my $success = 1;
    
    return $success;
  }
  
  static method readlink : string ($file : string) {
    if (Sys::OS->is_windows) {
      my $buffer_size = Sys::IO::Windows->get_readlink_buffer_size($file);
      my $buffer = (mutable string)new_string_len $buffer_size;
      Sys::IO::Windows->readlink($file, $buffer, $buffer_size);
      return $buffer;
    }
    else {
      my $buffer_size = Sys::IO->get_readlink_buffer_size($file);
      my $buffer = (mutable string)new_string_len $buffer_size;
      Sys::IO->readlink($file, $buffer, $buffer_size);
      return $buffer;
    }
  }
  
  static method symlink : int ($oldpath : string, $newpath : string) {
    if (Sys::OS->is_windows) {
      Sys::IO::Windows->symlink($oldpath, $newpath);
    }
    else {
      Sys::IO->symlink($oldpath, $newpath);
    }
    
    my $success = 1;
    
    return $success;
  }
  
  static method sleep : int ($seconds : int) {
    my $slept_number = Sys::Process->sleep($seconds);
    return $slept_number;
  }
  
  static method ioctl : int ($fd : int, $request : int, $request_arg_ref : object of byte[]|short[]|int[]|long[]|float[]|double[]|object = undef) {
    my $ioctl_ret = -1;
    
    if (Sys::OS->is_windows) {
      $ioctl_ret = Sys::Ioctl->ioctlsocket($fd, $request, (int[])$request_arg_ref);
    }
    else {
      $ioctl_ret = Sys::Ioctl->ioctl($fd, $request, $request_arg_ref);
    }
    
    return $ioctl_ret;
  }
  
  static method select : int ($readfds : Sys::Select::Fd_set, $writefds : Sys::Select::Fd_set, $exceptfds : Sys::Select::Fd_set, $timeout : Sys::Time::Timeval) {
    my $nfds = 1024;
    my $fd_number = Sys::Select->select($nfds, $readfds, $writefds, $exceptfds, $timeout);
    return $fd_number;
  }
  
  static method bind : int ($sockfd : int, $addr : Sys::Socket::Sockaddr) {
    my $addrlen = $addr->size;
    Sys::Socket->bind($sockfd, $addr, $addrlen);
    my $success = 1;
    return $success;
  }
  
  static method listen : int ($sockfd : int, $backlog : int) {
    Sys::Socket->listen($sockfd, $backlog);
    my $success = 1;
    return $success;
  }
  
  static method accept : Sys::Socket::Sockaddr ($new_sockfd_ref : int*, $sockfd : int) {
    my $addrlen = 128;
    
    my $addr = Sys::Socket::Sockaddr->new;
    
    $$new_sockfd_ref = Sys::Socket->accept($sockfd, $addr, \$addrlen);
    
    my $addr_child = Sys::Socket->to_family_sockaddr($addr);
    
    return $addr_child;
  }
  
  static method connect : int ($sockfd : int, $addr : Sys::Socket::Sockaddr) {
    my $addrlen = $addr->size;
    
    Sys::Socket->connect($sockfd, $addr, $addrlen);
    
    my $success = 1;
    
    return $success;
  }
  
  static method getpeername : Sys::Socket::Sockaddr ($sockfd : int) {
    my $addrlen = 128;
    
    my $addr = Sys::Socket::Sockaddr->new;
    
    Sys::Socket->getpeername($sockfd, $addr, \$addrlen);
    
    my $addr_child = Sys::Socket->to_family_sockaddr($addr);
    
    return $addr_child;
  }
  
  static method getsockname : Sys::Socket::Sockaddr ($sockfd : int) {
    
    my $addrlen = 128;
    
    my $addr = Sys::Socket::Sockaddr->new;
    
    Sys::Socket->getsockname($sockfd, $addr, \$addrlen);
    
    my $addr_child = Sys::Socket->to_family_sockaddr($addr);
    
    return $addr_child;
  }
  
  static method recv : int ($sockfd : int, $buf : mutable string, $len : int, $flags : int, $buf_offset : int = 0) {
    my $recv_length = Sys::Socket->recv($sockfd, $buf, $len, $flags, $buf_offset);
    
    return $recv_length;
  }
  
  static method send : int ($sockfd : int, $buf : string, $flags : int, $addr : Sys::Socket::Sockaddr = undef, $len : int = -1, $buf_offset : int = 0) {
    my $send_length = -1;
    
    if ($len < 0) {
      $len = length $buf;
    }
    
    if ($addr) {
      $send_length = Sys::Socket->sendto($sockfd, $buf, $len, $flags, $addr, $addr->size, $buf_offset);
    }
    else {
      $send_length = Sys::Socket->send($sockfd, $buf, $len, $flags, $buf_offset);
    }
    
    return $send_length;
  }
  
  static method shutdown : int ($sockfd : int, $how : int) {
    Sys::Socket->shutdown($sockfd, $how);
    
    my $success = 1;
    
    return $success;
  }
  
  static method socket : void ($sockfd_ref : int*, $domain : int, $type : int, $protocol : int) {
    $$sockfd_ref = Sys::Socket->socket($domain, $type, $protocol);
  }
  
  static method socketpair : int ($sock_fd1_ref : int*, $sock_fd2_ref : int*, $domain : int, $type : int, $protocol : int) {
    my $pair = new int[2];
    
    Sys::Socket->socketpair($domain, $type, $protocol, $pair);
    
    $$sock_fd1_ref = $pair->[0];
    
    $$sock_fd2_ref = $pair->[1];
    
    my $success = 1;
    
    return $success;
  }
  
  static method setsockopt : int ($sockfd : int, $level : int, $optname : int, $optval : object of string|Int) {
    
    unless ($optval) {
      die "\$optval must be defined.";
    }
    
    if ($optval is_type Int) {
      my $optval_ref = [$optval->(Int)->value];
      my $optval_string = new_string_len 4;
      Fn->memcpy($optval_string, 0, $optval_ref, 0, 4);
      
      Sys::Socket->setsockopt($sockfd, $level, $optname, $optval_string, 4);
    }
    elsif ($optval is_type string) {
      my $optval_string = (string)$optval;
      Sys::Socket->setsockopt($sockfd, $level, $optname, $optval_string, length $optval_string);
    }
    else {
      die "The type of \$optval must be Int or string.";
    }
    
    my $success = 1;
    
    return $success;
  }
  
  static method getsockopt : string ($sockfd : int, $level : int, $optname : int, $optlen : int = -1) {
    
    unless ($optlen >= 0) {
      $optlen = 4;
    }
    
    my $optval = (mutable string)new_string_len $optlen;
    
    Sys::Socket->getsockopt($sockfd, $level, $optname, $optval, \$optlen);
    
    my $success = 1;
    
    if ($success) {
      return $optval;
    }
    else {
      return undef;
    }
  }
  
  static method truncate : int ($fd : int, $legnth : long) {
    
    Sys::IO->ftruncate($fd, $legnth);
    
    my $success = 1;
    
    return $success;
  }
  
  static method sysread : int ($fd : int, $buf : mutable string, $count : int, $buf_offset : int = 0) {
    my $read_length = Sys::IO->read($fd, $buf, $count, $buf_offset);
    return $read_length;
  }
  
  static method syswrite : int ($fd : int, $buf : string, $count : int = -1, $buf_offset : int = 0) {
    my $write_length = Sys::IO->write($fd, $buf, $count, $buf_offset);
    return $write_length;
  }
  
  static method eof : int ($stream : Sys::IO::FileStream) {
    my $eof = Sys::IO->feof($stream);
    return $eof;
  }
  
  static method fileno : int ($stream : Sys::IO::FileStream) {
    my $fd = Sys::IO->fileno($stream);
    return $fd;
  }
  
  precompile static method readline : mutable string ($stream : Sys::IO::FileStream) {
    
    unless ($stream) {
      die("\$stream must be defined.");
    }
    
    my $capacity = 80;
    my $buf = (mutable string)new_string_len $capacity;
    
    my $pos = 0;
    my $end_is_eof = 0;
    my $eof = IO->EOF;
    while (1) {
      my $ch = Sys->getc($stream);
      if ($ch == $eof) {
        $end_is_eof = 1;
        last;
      }
      else {
        if ($pos >= $capacity) {
          my $new_capacity = $capacity * 2;
          my $new_buf = (mutable string)new_string_len $new_capacity;
          
          Fn->memcpy($new_buf, 0, $buf, 0, $capacity);
          
          $capacity = $new_capacity;
          $buf = $new_buf;
        }
        
        if ($ch == '\n') {
          $buf->[$pos] = (byte)$ch;
          $pos++;
          last;
        }
        else {
          $buf->[$pos] = (byte)$ch;
          $pos++;
        }
      }
    }
    
    my $line = (mutable string)undef;
    if ($pos > 0 || !$end_is_eof) {
      if ($pos == 0) {
        $line = (mutable string)new_string_len 0;
      }
      else {
        $line = (mutable string)new_string_len $pos;
        Fn->memcpy($line, 0, $buf, 0, $pos);
      }
    }
    
    return $line;
  }
  
  static method getc : int ($stream : Sys::IO::FileStream) {
    my $char = Sys::IO->getc($stream);
    return $char;
  }
  
  static method flock : int ($fd : int, $operation : int) {
    Sys::IO->flock($fd, $operation);
    my $success = 1;
    return $success;
  }
  
  static method print : int ($stream : Sys::IO::FileStream, $string : string) {
    my $write_length = Sys::IO->fwrite($string, 1, length $string, $stream);
    my $success = $write_length >= 0;
    return $success;
  }
  
  static method printf : int ($stream : Sys::IO::FileStream, $format : string, $args : object[]) {
    my $formated_string = Format->sprintf($format, $args);
    my $success = &print($stream, $formated_string);
    return $success;
  }
  
  static method read : int ($stream : Sys::IO::FileStream, $buf : mutable string, $count : int, $buf_offset : int = 0) {
    my $read_length = Sys::IO->fread($buf, 1, $count, $stream);
    return $read_length;
  }
  
  static method rewinddir : void ($dirp : Sys::IO::DirStream) {
    Sys::IO->rewinddir($dirp);
  }
  
  static method readdir : Sys::IO::Dirent ($dirp : Sys::IO::DirStream) {
    my $next_dirp = Sys::IO->readdir($dirp);
    return $next_dirp;
  }
  
  static method seek : int ($stream : Sys::IO::FileStream, $offset : long, $whence : int) {
    Sys::IO->fseek($stream, $offset, $whence);
    my $success = 1;
    return $success;
  }
  
  static method sysseek : long ($fd : int, $offset : long, $whence : int) {
    my $result_offset = Sys::IO->lseek($fd, $offset, $whence);
    return $result_offset;
  }
  
  static method tell : long ($stream : Sys::IO::FileStream) {
    my $current_position = Sys::IO->ftell($stream);
    return $current_position;
  }
  
  static method telldir : long ($dirp : Sys::IO::DirStream) {
    my $current_position = Sys::IO->telldir($dirp);
    return $current_position;
  }
  
  static method lstat : Sys::IO::Stat ($path : string) {
    my $stat = Sys::IO::Stat->new;
    Sys::IO::Stat->lstat($path, $stat);
    return $stat;
  }
  
  static method utime : int ($atime : long, $mtime : long, $filename : string) {
    
    my $utimbuf = (Sys::IO::Utimbuf)undef;
    
    if ($atime < 0 && $mtime < 0) {
      # Do nothing
    }
    else {
      $utimbuf = Sys::IO::Utimbuf->new;
      $utimbuf->set_actime($atime);
      $utimbuf->set_modtime($mtime);
    }
    
    Sys::IO->utime($filename, $utimbuf);
    my $success = 1;
    return $success;
  }
  
  static method chown : int ($owner : int, $group : int, $path : string) {
    Sys::IO->chown($path, $owner, $group);
    my $success = 1;
    return $success;
  }
  
  static method fcntl : int ($fd : int, $command : int, $command_arg : object of Int|Sys::IO::Flock|object = undef) {
    my $fcntl_ret = Sys::IO->fcntl($fd, $command, $command_arg);
    return $fcntl_ret;
  }
  
  static method open : int ($stream_ref : Sys::IO::FileStream[], $open_mode : string, $file_name : string) {
    
    unless ($stream_ref) {
      die "\$stream_ref must be defined.";
    }
    
    unless (@$stream_ref == 1) {
      die "The length of \$stream_ref must be equal to 1.";
    }
    
    if ($open_mode eq "<") {
      $open_mode = "rb";
    }
    elsif ($open_mode eq ">") {
      $open_mode = "wb";
    }
    elsif ($open_mode eq ">>") {
      $open_mode = "wa";
    }
    elsif ($open_mode eq "+<") {
      $open_mode = "r+b";
    }
    elsif ($open_mode eq "+>") {
      $open_mode = "w+b";
    }
    elsif ($open_mode eq "+>>") {
      $open_mode = "a+b";
    }
    
    my $stream = Sys::IO->fopen($file_name, $open_mode);
    
    $stream_ref->[0] = $stream;
    
    my $success = $stream != undef;
    
    return $success;
  }
  
  static method fdopen : int ($stream_ref : Sys::IO::FileStream[], $open_mode : string, $fd : int) {
    
    unless ($stream_ref) {
      die "\$stream_ref must be defined.";
    }
    
    unless (@$stream_ref == 1) {
      die "The length of \$stream_ref must be equal to 1.";
    }
    
    if ($open_mode eq "<") {
      $open_mode = "rb";
    }
    elsif ($open_mode eq ">") {
      $open_mode = "wb";
    }
    elsif ($open_mode eq ">>") {
      $open_mode = "wa";
    }
    elsif ($open_mode eq "+<") {
      $open_mode = "r+b";
    }
    elsif ($open_mode eq "+>") {
      $open_mode = "w+b";
    }
    elsif ($open_mode eq "+>>") {
      $open_mode = "a+b";
    }
    
    my $stream = Sys::IO->fdopen($fd, $open_mode);
    
    $stream_ref->[0] = $stream;
    
    my $success = $stream != undef;
    
    return $success;
  }
  
  static method sysopen : int ($fd_ref : int*, $path : string, $flags : int, $mode : int = 0) {
    
    my $fd = Sys::IO->open($path, $flags, $mode);
    
    $$fd_ref = $fd;
    
    my $success = $fd >= 0;
    
    return $success;
  }
  
  static method say : int ($stream : Sys::IO::FileStream, $string : string) {
    my $success = &print($stream, "$string\n");
    return $success;
  }
  
  static method STDIN : Sys::IO::FileStream () { return Sys::IO->spvm_stdin; }
  
  static method STDOUT : Sys::IO::FileStream () { return Sys::IO->spvm_stdout; }
  
  static method STDERR : Sys::IO::FileStream () { return Sys::IO->spvm_stderr; }
  
  static method signal : Sys::Signal::Handler ($signum : int, $handler_name : string) {
    
    my $handler = (Sys::Signal::Handler)undef;
    
    if ($handler_name eq "IGNORE") {
      $handler = Sys::Signal->SIG_IGN;
    }
    elsif ($handler_name eq "DEFAULT") {
      $handler = Sys::Signal->SIG_DFL;
    }
    else {
      die "\$handler_name \"$handler_name\" is not available.";
    }
    
    my $old_handler = Sys::Signal->signal($signum, $handler);
    
    return $old_handler;
  }
  
  static method kill : int ($sig : int, $pid : int) {
    Sys::Signal->kill($pid, $sig);
    
    my $success = 1;
    
    return $success;
  }
  
  static method pipe : int ($pipe_fds : int[]) {
    
    if (Sys::OS->is_windows) {
      Sys::Process->_pipe($pipe_fds, 0, IO->O_BINARY);
    }
    else {
      Sys::Process->pipe($pipe_fds);
      
      Sys->fcntl($pipe_fds->[0], IO->F_SETFD, IO->FD_CLOEXEC);
      
      Sys->fcntl($pipe_fds->[1], IO->F_SETFD, IO->FD_CLOEXEC);
    }
    
    my $success = 1;
    
    return $success;
  }
}
